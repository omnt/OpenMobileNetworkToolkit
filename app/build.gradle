/*
 * SPDX-FileCopyrightText: 2023 Peter Hasse <peter.hasse@fokus.fraunhofer.de>
 * SPDX-FileCopyrightText: 2023 Fraunhofer FOKUS
 *
 * SPDX-License-Identifier: apache2
 */

plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id("org.spdx.sbom") version "0.7.0"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('android/key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    project.logger.warn("File not found: ${keystorePropertiesFile}")
    project.logger.warn("Using default values for signing config.")
    keystoreProperties['keyAlias'] = "omnt_default"
    keystoreProperties['keyPassword'] = "omnt2024"
    keystoreProperties['storeFile'] = file("omnt_default.jks")
    keystoreProperties['storePassword'] = "omnt2024"
}


android {
    signingConfigs {
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdk 34

    defaultConfig {
        applicationId "de.fraunhofer.fokus.OpenMobileNetworkToolkit"
        //targetSdkPreview "VanillaIceCream"
        minSdk 31
        targetSdk 34
        versionCode 2
        versionName "0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'de.fraunhofer.fokus.OpenMobileNetworkToolkit'
    ndkVersion '25.2.9519653'
    lint {
        abortOnError false
    }
    buildToolsVersion '34.0.0'
}

spdxSbom {
    targets {
        register("release") {
            configurations = ['releaseRuntimeClasspath']
            scm {
                uri.set("https://github.com/omnt/OpenMobileNetworkToolkit")
                revision.set("0.2")
            }
            document {
                name.set("OpenMobileNetworkToolkit")
                namespace.set("de.fraunhofer.fokus.OpenMobileNetworkToolkit")
                creator.set("Person: NGNI")
                packageSupplier.set("Organization: Fraunhofer FOKUS NGNI")
            }
        }
    }
}

dependencies {
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.work:work-runtime:2.9.0'
    def room_version = "2.6.1"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.guava:guava:33.1.0-jre'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'
    implementation 'androidx.activity:activity:1.9.0'
    implementation 'androidx.fragment:fragment:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.7.7'
    implementation 'androidx.navigation:navigation-ui:2.7.7'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'com.influxdb:influxdb-client-java:7.0.0'
    implementation 'com.google.android.gms:play-services-location:21.2.0'
    implementation 'com.github.anastr:speedviewlib:1.6.1'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
}

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}